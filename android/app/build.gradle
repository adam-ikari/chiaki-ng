apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'

def rootCMakeLists = "../../CMakeLists.txt"
def rootCMakeListsContent = file(rootCMakeLists).text
static def grepVersionComponent(content, varname) {
    def match = content =~ /set\($varname ([0-9]+)\)/
    if(!match.find())
        throw new GradleException("Failed to find $varname in CMakeLists.txt")
    return match.group(1)
}
def chiakiVersionMajor = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_MAJOR")
def chiakiVersionMinor = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_MINOR")
def chiakiVersionPatch = grepVersionComponent(rootCMakeListsContent, "CHIAKI_VERSION_PATCH")
def chiakiVersion = "$chiakiVersionMajor.$chiakiVersionMinor.$chiakiVersionPatch"
println("Determined Chiaki Version: $chiakiVersion")

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.2"
    ndkVersion "25.1.8937393"  // 使用与Gradle插件7.x兼容的NDK版本
    defaultConfig {
        applicationId "com.metallic.chiaki"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 11
        versionName chiakiVersion
        
        // 配置NDK构建
        externalNativeBuild {
            cmake {
                version "3.18.1" // 使用与NDK 21.4兼容的稳定CMake版本
                cppFlags "-std=c++14"
                arguments "-DANDROID_STL=c++_shared",
                        "-DCHIAKI_ENABLE_ANDROID=ON",
                        "-DCHIAKI_LIB_ENABLE_MBEDTLS=ON",
                        "-DCHIAKI_LIB_MBEDTLS_EXTERNAL_PROJECT=ON",
                        "-DCMAKE_BUILD_TYPE=Release"
            }
        }
        
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
        // 禁用可能导致Java 17模块系统问题的实验性功能
        freeCompilerArgs += ["-Xjvm-default=compatibility"]
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }


    Properties properties = new Properties()
    def propertiesFile = file("../local.properties")
    if (propertiesFile.exists()) {
        properties.load(propertiesFile.newDataInputStream())
    }

	if(properties.containsKey("chiakiKeystore")) {
        println("Enabling Local Signing")
        buildTypes.release.signingConfig = signingConfigs.release
        buildTypes.debug.signingConfig = signingConfigs.release
        signingConfigs.release.storeFile = file(properties.get("chiakiKeystore"))
        signingConfigs.release.storePassword = properties.get("chiakiKeystorePW")
        signingConfigs.release.keyAlias = properties.get("chiakiKeyAlias")
        signingConfigs.release.keyPassword = properties.get("chiakiKeyPW")
    } else {
        println("Signing not enabled")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += "-Xuse-experimental=kotlin.ExperimentalUnsignedTypes"
        freeCompilerArgs += "-Xsuppress-version-warnings"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version") {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk8'
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-stdlib-jdk7'
    }
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.core:core-ktx:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.6.1'
    implementation "io.reactivex.rxjava3:rxjava:3.1.6"
    implementation "io.reactivex.rxjava3:rxkotlin:3.0.1"
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.2'
    def room_version = "2.5.0"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-rxjava3:$room_version"  // 更新到RxJava3
    kapt "androidx.room:room-compiler:$room_version"
    implementation "com.squareup.moshi:moshi:1.14.0"
    // 强制统一lifecycle依赖版本
    implementation('androidx.lifecycle:lifecycle-common:2.6.1') {
        force = true
    }
    implementation('androidx.lifecycle:lifecycle-process:2.6.1') {
        force = true
    }
}